// GAME_IS doesn't support variables
DEFINE_PATCH_MACRO read_item_sources_2da BEGIN
  READ_2DA_ENTRIES_NOW ilist 2
  FOR (i=1;i<ilist;++i) BEGIN
    READ_2DA_ENTRY_FORMER ilist i 0 item
    READ_2DA_ENTRY_FORMER ilist i 1 isource
    // auto formatting workaround
    INNER_PATCH_SAVE source ~%isource%~ BEGIN
      REPLACE_TEXTUALLY CASE_INSENSITIVE ~|$~ ~~
    END
    PATCH_IF NOT (~%item%~ STRING_MATCHES_REGEXP ~^//~ == 0) // skip comments
      AND NOT (~%item%~ STR_EQ ~|~) // auto formatting workaround
    BEGIN
      SET $item_source(~%item%~ ~%source%~) = 1 // global list of items and sources
      SET $items(~%item%~) = 1 // global list of items
    END
  END
END
DEFINE_ACTION_MACRO read_item_sources BEGIN
  COPY - ~%MOD_FOLDER%/items/strict_%game%.bsv~ ~%MOD_FOLDER%-inlined/strict_%game%.bsv~
    LPM read_item_sources_2da
  BUT_ONLY
  ACTION_IF expanded == 1 BEGIN
    COPY - ~%MOD_FOLDER%/items/expanded_%game%.bsv~ ~%MOD_FOLDER%-inlined/expanded_%game%.bsv~
      LPM read_item_sources_2da
    BUT_ONLY
  END
  ACTION_IF reduce_wands == 1 BEGIN
    COPY - ~%MOD_FOLDER%/items/wands_%game%.bsv~ ~%MOD_FOLDER%-inlined/wands_%game%.bsv~
      LPM read_item_sources_2da
    BUT_ONLY
  END
END

ACTION_IF GAME_INCLUDES ~bg1~ BEGIN
  OUTER_SPRINT game ~bg1~
  LAM read_item_sources
END
ACTION_IF GAME_INCLUDES ~bg2~ BEGIN
  OUTER_SPRINT game ~bg2~
  LAM read_item_sources
END

// filter compat, skip items and sources that aren't present in current selection
ACTION_PHP_EACH item_source_compat AS i_s => should_be_present BEGIN
  OUTER_SPRINT item $i_s(0)
  OUTER_SPRINT source $i_s(1)
  ACTION_IF VARIABLE_IS_SET $sources(~%source%~) BEGIN
    OUTER_SET $item_source(~%item%~ ~%source%~) = $item_source_compat(~%item%~ ~%source%~)
  END
END

// custom replacement list, just so creatures still have some weapons and armor
INCLUDE ~%MOD_FOLDER%/core/cre_replacement.tpa~

// check if item is present in all sources
ACTION_PHP_EACH item_source AS i_s => should_be_present BEGIN
  OUTER_SPRINT item $i_s(0)
  OUTER_SPRINT source $i_s(1)
  OUTER_SET skip = 0
  ACTION_IF should_be_present == 0 BEGIN
    PRINT ~%item% was moved from %source% by another mod, skipping this source~
    OUTER_SET skip = 1
  END
  ACTION_IF VARIABLE_IS_SET $item_present(~%item%~)
    AND $item_present(~%item%~) == 0
  BEGIN
    PRINT ~%item% was checked already and is missing from another resource, skipping~
    OUTER_SET skip = 1
  END
  ACTION_IF skip == 0 BEGIN
    LAF IS_ITEM_IN_RESOURCE STR_VAR item = ~%item%~ resource = ~%source%~ RET present END 
    OUTER_SET $item_present(~%item%~) = present
    ACTION_IF present == 1 BEGIN
      OUTER_SET $csources(~%source%~) = 1 // list of all custom sources
    END ELSE BEGIN
      WARN ~UA WARNING: %item% is missing from %source%, skipping!~
    END
  END
END

// init custom regex for each source
ACTION_PHP_EACH csources AS csource => int BEGIN // this is already filtered: only sources with present items
  OUTER_SPRINT $custom_regex(~%csource%~) ~~
END

// build a regex of all items, skip those that are not found in ALL canon sources
OUTER_SPRINT common_regex ~~
ACTION_PHP_EACH item_present AS item => present BEGIN
  ACTION_IF present == 1 BEGIN

    // common regex
    ACTION_IF ~%common_regex%~ STR_EQ ~~ BEGIN //empty, initialize
      OUTER_SPRINT common_regex ~%item%~
    END ELSE BEGIN
      OUTER_SPRINT common_regex ~%common_regex%\|%item%~ //append
    END
    // custom regex for each source
    ACTION_PHP_EACH csources AS csource => int BEGIN
      ACTION_IF
        NOT VARIABLE_IS_SET $item_source(~%item%~ ~%csource%~)
        OR $item_source(~%item%~ ~%csource%~) != 1 // may be changed in compat section
      BEGIN
        OUTER_SPRINT tmp $custom_regex(~%csource%~)
        ACTION_IF ~%tmp%~ STR_EQ ~~ BEGIN
          OUTER_SPRINT $custom_regex(~%csource%~) ~%item%~
        END ELSE BEGIN
          OUTER_SPRINT $custom_regex(~%csource%~) ~%tmp%\|%item%~
        END
      END
    END

  END
END
